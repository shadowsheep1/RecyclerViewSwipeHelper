apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version '1.0.0-rc1'
group 'it.shadowsheep.recyclerviewswipehelper'


//  ./gradlew clean build bintrayUpload --info
bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['Production']
    configurations = ['archives']
    override = true
    pkg {
        repo = 'RecyclerViewSwipeHelper'
        name = 'RecyclerViewSwipeHelper'
        description = "An Android library to add swipe action buttons to a RecyclerView"
        publicDownloadNumbers = true
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/shadowsheep1/RecyclerViewSwipeHelper'
        version {
            name = this.version
            desc = "Version ${this.version}"
            released = new Date()
            vcsTag = this.version
        }
    }
}

// In order to link to JCenter we need to add sources
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/recyclerviewswipehelper-release.aar")  {
                builtBy tasks.getByName("assembleRelease")
            }
            groupId
            artifactId 'recyclerviewswipehelper'
            version this.version

            //The publication doesn't know about our dependencies, so we have to manually add them to the pom
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}
